buildscript {

  ext {
    kotlin_version = "1.5.31"
    appName = "InfiniteBootleg"
    gdxVersion = '1.10.0'
    roboVMVersion = '2.3.6'
    box2DLightsVersion = '1.5'
    ashleyVersion = '1.7.3'
    aiVersion = '1.8.2'
    powermockVersion = '2.0.2'
    visVersion = '1.5.0'
    ktxVersion = "1.10.0-b1"
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    gradlePluginPortal()
    google()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.ALL
}

def getGitVersion() {
  def file = new File(project.projectDir, "core/assets/version")
  if (!file.exists() || !file.canRead()) {
    System.err.println("Failed to read version from file " + file.absolutePath)
    return "UNKNOWN"
  }
  return file.readLines("UTF_8")[0]
}

allprojects {
  apply plugin: "java"
  apply plugin: 'idea'
  apply plugin: 'kotlin'

  version = getGitVersion()
}

test {
  failFast = true
}


subprojects {

  sourceCompatibility = 16
  targetCompatibility = sourceCompatibility
  [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = sourceCompatibility

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  sourceSets.test.java.srcDirs = ["src/test/java", "src/test/kotlin"]


  repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://raw.githubusercontent.com/kh498/maven2/repo/" }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  }

  test {
//    failFast = true
//    finalizedBy jacocoTestReport
    useJUnitPlatform {
      includeEngines "junit-jupiter", "junit-vintage"
    }
    jvmArgs = ["--add-opens", "java.base/jdk.internal.loader=ALL-UNNAMED",
               "--add-opens", "java.xml/jdk.xml.internal=ALL-UNNAMED",
               "--add-opens", "java.base/java.lang=ALL-UNNAMED",
               "--add-opens", "java.base/java.io=ALL-UNNAMED",
               "--add-opens", "java.base/java.util.concurrent=ALL-UNNAMED",
               "--add-opens", "java.base/java.util.stream=ALL-UNNAMED",
               "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED",
               "--add-opens", "java.base/java.security=ALL-UNNAMED",
               "--add-opens", "java.base/sun.nio.fs=ALL-UNNAMED",
               "--add-opens", "jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
               "--add-opens", "jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
               "--add-opens", "jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
               "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
               "--add-opens", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
    ]
  }

}

project(":desktop") {

  dependencies {
    implementation project(":core")

    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
  }
}

project(":core") {

  dependencies {

    implementation "com.strongjoshua:libgdx-inGameConsole:1.0.0"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "no.kh498.util:CommonUtil:1.0.4"

    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"


    api "io.github.libktx:ktx-app:$ktxVersion"
    implementation "io.github.libktx:ktx-vis:$ktxVersion"
    implementation "io.github.libktx:ktx-vis-style:$ktxVersion"
    implementation "io.github.libktx:ktx-style:$ktxVersion"
    implementation "io.github.libktx:ktx-actors:$ktxVersion"
    implementation "io.github.libktx:ktx-collections:$ktxVersion"
    implementation "io.github.libktx:ktx-graphics:$ktxVersion"
    implementation "io.github.libktx:ktx-async:$ktxVersion"
    implementation "io.github.libktx:ktx-assets:$ktxVersion"

    implementation "com.kotcrab.vis:vis-ui:$visVersion"
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'

    implementation 'com.google.protobuf:protobuf-java:3.18.1'
    implementation 'io.netty:netty-all:4.1.69.Final'

    implementation 'org.jetbrains:annotations:22.0.0'

    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation 'io.mockk:mockk:1.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.1'
    testImplementation "org.mockito:mockito-all:1.9.+"
    testRuntimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  }
}
